# -*- python -*-
import os, os.path
Import('env libhammer_shared')

phpenv = env.Clone(IMPLICIT_COMMAND_DEPENDENCIES = 0)

phpenv.Append(CPPPATH = ['../../', '/usr/include/php5', '/usr/include/php5/main', '/usr/include/php5/Zend', '/usr/include/php5/TSRM'])
phpenv.Append(CCFLAGS = ['-fpic', '-DSWIG', '-Wno-all', '-Wno-extra', '-Wno-error'])
phpenv.Append(LIBS = ['hammer'])
phpenv.Append(LIBPATH = ['../../']) 

swig_src = phpenv.Command("hammer.i", "../swig/hammer.i", Copy("$TARGET", "$SOURCE"))
bindings_src = phpenv.Command(['hammer.php', 'hammer_wrap.c', 'php_hammer.h'], 'hammer.i', 'swig -php -DHAMMER_INTERNAL__NO_STDARG_H -Isrc/ $SOURCE')
libhammer_php = phpenv.SharedLibrary('hammer', ['hammer_wrap.c'])
Default(swig_src, bindings_src, libhammer_php)

phptestenv = phpenv.Clone()
phptestenv['ENV']['LD_LIBRARY_PATH'] = os.path.dirname(str(libhammer_shared[0]))
phptests = ['Tests']
testphp = phptestenv.Alias("testphp", phptestenv.Command(phptests, [libhammer_php], "phpunit -v --include-path "+os.getcwd()+" $TARGET"))
env['testruns'].append(testphp)
print "Testing: " + str([str(x[0]) for x in env['testruns']])

